set(
    BINARY_MODEL_DATA
    gb_char.bin
    gbk_char.bin
    phrase_index.bin
    pinyin_index.bin
    addon_phrase_index.bin
    addon_pinyin_index.bin
    bigram.db
)

set(
    BINARY_MODEL_DATA_FILES
    ${CMAKE_BINARY_DIR}/data/gb_char.bin
    ${CMAKE_BINARY_DIR}/data/gbk_char.bin
    ${CMAKE_BINARY_DIR}/data/phrase_index.bin
    ${CMAKE_BINARY_DIR}/data/pinyin_index.bin
    ${CMAKE_BINARY_DIR}/data/bigram.db
)

set(
    gen_binary_files_BIN
    ${DATA_UTILS_STORAGE_PATH}/gen_binary_files
)

set(
    import_interpolation_BIN
    ${DATA_UTILS_STORAGE_PATH}/import_interpolation
)

set(
    gen_unigram_BIN
    ${DATA_UTILS_TRAINING_PATH}/gen_unigram
)

add_custom_target(
    data
    ALL
    DEPENDS
        ${BINARY_MODEL_DATA}
)

if(DOWNLOAD_MODEL_DATA_ARCHIVE)
    add_custom_command(
        OUTPUT
            ${CMAKE_SOURCE_DIR}/data/gb_char.table
            ${CMAKE_SOURCE_DIR}/data/gbk_char.table
            ${CMAKE_SOURCE_DIR}/data/interpolation2.text
            ${CMAKE_SOURCE_DIR}/data/table.conf
        COMMENT
            "Downloading textual model data..."
        COMMAND
        wget -P ${CMAKE_SOURCE_DIR} http://downloads.sourceforge.net/libpinyin/models/model19.text.tar.gz
        COMMAND
        tar xvf ${CMAKE_SOURCE_DIR}/model19.text.tar.gz -C ${CMAKE_SOURCE_DIR}/data
    )
else()
    add_custom_command(
        OUTPUT
            ${CMAKE_SOURCE_DIR}/data/gb_char.table
            ${CMAKE_SOURCE_DIR}/data/gbk_char.table
            ${CMAKE_SOURCE_DIR}/data/interpolation2.text
            ${CMAKE_SOURCE_DIR}/data/table.conf
        COMMENT
            "Checking if model data is provided in ${CMAKE_SOURCE_DIR}/model19.text.tar.gz"
        COMMAND
        test -f ${CMAKE_SOURCE_DIR}/model19.text.tar.gz
        COMMAND
        tar xvf ${CMAKE_SOURCE_DIR}/model19.text.tar.gz -C ${CMAKE_SOURCE_DIR}/data
    )
endif()

add_custom_command(
    OUTPUT
        gb_char.bin
        gbk_char.bin
        phrase_index.bin
        pinyin_index.bin
        addon_phrase_index.bin
        addon_pinyin_index.bin
    COMMENT
        "Building binary model data..."
    COMMAND
        ${gen_binary_files_BIN} --table-dir ${CMAKE_SOURCE_DIR}/data
    DEPENDS
        gen_binary_files
        ${CMAKE_SOURCE_DIR}/data/gb_char.table
        ${CMAKE_SOURCE_DIR}/data/gbk_char.table
        ${CMAKE_SOURCE_DIR}/data/table.conf
)

add_custom_command(
    OUTPUT
        bigram.db
    COMMENT
        "Building binary bigram data..."
    COMMAND
        ${import_interpolation_BIN} --table-dir ${CMAKE_SOURCE_DIR}/data < ${CMAKE_SOURCE_DIR}/data/interpolation2.text
    COMMAND
        ${gen_unigram_BIN} --table-dir ${CMAKE_SOURCE_DIR}/data
    DEPENDS
        import_interpolation
        ${CMAKE_SOURCE_DIR}/data/interpolation2.text
        ${CMAKE_BINARY_DIR}/data/gb_char.bin
        ${CMAKE_BINARY_DIR}/data/gbk_char.bin
        ${CMAKE_BINARY_DIR}/data/phrase_index.bin
        ${CMAKE_BINARY_DIR}/data/pinyin_index.bin
)

install(
    FILES
        ${BINARY_MODEL_DATA_FILES}
    DESTINATION
        ${DIR_SHARE_LIBPINYIN}/data
)

set_directory_properties(
    PROPERTIES
        ADDITIONAL_MAKE_CLEAN_FILES
            ${BINARY_MODEL_DATA_FILES}
)

configure_file(
  table.conf.in
  table.conf
  @ONLY
)
